/*
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++WWWWWWWWWWWW+++++++++++++++++++++++++++++
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++WWWWWWWWWWWW+++++++++++++++++++++++++++++
+++++++++++++++++++##++++++++++++++++##+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++WWWWWW  WWWWWWWW+++++++++++++++++++++++++++
+++++++++++++++++++##++++++++++++++++##+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++WWWWWW  WWWWWWWW+++++++++++++++++++++++++++
+++++++++++++++++++###++++++++++++++###+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++WWWWWWWWWW  WWWWWWWW+++++++++++++++++++++++++
+++++++++++++++++++###++++++++++++++###+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++WWWWWWWWWW  WWWWWWWW+++++++++++++++++++++++++
+++++++++++++++++++####++++++++++++####+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++WWWWWWWWWWWWWWWWWWWW+++++++++++++++++++++++++
++++++++++++++++++#####++++++++++++####+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++WWWWWWWWWWWWWWWWWWWW+++++++++++++++++++++++++
++++++++++++++++++######++++++++++#####*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++....................+++++++++++++++++++++++++
++++++++++++++++++######++++++++++######++++++++++++++++++++++++++++++++++++++++++++++++++++++++++....................+++++++++++++++++++++++++
++++++++++++++++++#######++++++++#######++++++++++++++++++++++++++++++++++++++++++++++++++++++++......::::......::::....+++++++++++++++++++++++
++++++++++++++++++##+####++++++++##+####++++++++++++++++++++++++++++++++++++++++++++++++++++++++......::::......::::....+++++++++++++++++++++++
++++++++++++++++++##+#####++++++###+####++++++++++++++++++++++++++++++++++++++++++++++++++++++++....::****::..::****....+++++++++++++++++++++++
+++++++++++++++++*##++####++++++##++####+########*++++++++++++++++++++++++++++++++++++++++++++++....::****::..::****....+++++++++++++++++++++++
+++++++++++++++++###++#####++++###+++###+############++++++++++++++++WW++WW+++++++++++++++++++....::::WW::::::::WW::......+++++++++++++++++++++
+++++++++++++++++###+++####++++##++++####++++++++####++++++++++++++++WW+#W++++++++++++++++++++....::::WW::::::::WW::......+++++++++++++++++++++
+++++++++++++++++##++++#####++###++#+####++++++++++##+++++++++++++++++WWWW++++++++++++++++++++......::::::::::::::::......+++++++++++++++++++++
+++++++++++++++++##+++++####++##++##+####+++++++++++#+++++++++++++++++*WW+++++++++++++++++++++......::::::::::::::::......+++++++++++++++++++++
+++++++++++++++++##+++++########+###+####+++++++++++#+++++++++++++++++WWW+++++++++++++++++++++......::::::::::::::::......+++++++++++++++++++++
+++++++++++++++++##++++++######++###+####+++++++++++#+++++++++++++++++W*WW++++++++++++++++++++......::::::::::::::::......+++++++++++++++++++++
++++++++++++++++###++++++######+####+####++++++++++++++++++++++++++++WW++W#+++++++++++++++++++......::::::::WW::::::......+++++++++++++++++++++
++++++++++++++++###+++++++####++####+####+++++++++++++++++++++++++++#W+++WW+++++++++++++++++++......::::::::WW::::::......+++++++++++++++++++++
++++++++++++++++###+++++++*###++####++####++++++++++++++++++++++++++++++++++++++++++++++++++++......::::::::::::::::......+++++++++++++++++++++
++++++++++++++++###++++++++##+++####++####++++++++++++++++++++++++++++++++++++++++++++++++++++......::::::::::::::::......+++++++++++++++++++++
++++++++++++++#######++++++++++#####+*#######+++++++++++++++++++++++++++++++++++++++++++++++++++....::::::WWWWWW::::....+++++++++++++++++++++++
+++++++++++++++++++++++++++++++*####++++++++++++#######+++++++++++++++++++++++++++++++++++++++++....::::::WWWWWW::::....+++++++++++++++++++++++
++++++++++++++++++++++++++++++++####+++++++++++++#####++++++++++++++++++++++++++++++++++++++++++......::::::::::::......+++++++++++++++++++++++
++++++++++++++++++++++++++++++++#####++++++++++++####*++++++++++++++++++++++++++++++++++++++++++......::::::::::::......+++++++++++++++++++++++
++++++++++++++++++++++++++++++++#####++++++++++++####*++++++++++++++++++++++++++++++++++++++++++++....::WW::::::WW....+++++++++++++++++++++++++
++++++++++++++++++++++++++++++++#####++++++++++++####+++++++++++++++++++++++++++++++++++++++++++++....::WW::::::WW....+++++++++++++++++++++++++
+++++++++++++++++++++++++++++++++#####+++++++++++####+++++++++++++++++++++++++++++++++++++++++++++++WW::::WWWWWW+++++++++++++++++++++++++++++++
++++++++++++++++++++++++++++++++++#####++++++++++####+++++++++++++++++++++++++++++++++++++++++++++++WW::::WWWWWW+++++++++++++++++++++++++++++++
++++++++++++++++++++++++++++++++++######+++++++++####+++++++++++++++++++++++++++++++++++++++++++++++WW::::::WW+++++++++++++++++++++++++++++++++
++++++++++++++++++++++++++++++++++++######+++++++####+++++++++++++++++++++++++++++++++++++++++++++++WW::::::WW+++++++++++++++++++++++++++++++++
+++++++++++++++++++++++++++++++++++++########+++#####+++++++++++++++++++++++++++++++++++++++++++++++WW::::::WW+++++++++++++++++++++++++++++++++
++++++++++++++++++++++++++++++++++++++++############++++++++++++++++++++++++++++++++++++++++++++++++WW::::::WW+++++++++++++++++++++++++++++++++
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++                                                                                                                                        
*/
// SPDX-License-Identifier: MIT
// Contract authored by August Rosedale (@augustfr)
// Originally writen for Mirage Gallery Curated drop with Claire Silver (@clairesilver12)
// https://miragegallery.ai
 
pragma solidity ^0.8.15;
 
import "@openzeppelin/contracts/access/Ownable.sol";
 
interface curatedContract {
 function projectIdToArtistAddress(uint256 _projectId) external view returns (address payable);
 function projectIdToPricePerTokenInWei(uint256 _projectId) external view returns (uint256);
 function projectIdToAdditionalPayee(uint256 _projectId) external view returns (address payable);
 function projectIdToAdditionalPayeePercentage(uint256 _projectId) external view returns (uint256);
 function mirageAddress() external view returns (address payable);
 function miragePercentage() external view returns (uint256);
 function mint(address _to, uint256 _projectId, address _by) external returns (uint256 tokenId);
 function earlyMint(address _to, uint256 _projectId, address _by) external returns (uint256 _tokenId);
 function balanceOf(address owner) external view returns (uint256);
}
 
interface membershipContracts {
 function balanceOf(address owner, uint256 _id) external view returns (uint256);
}
 
contract mirageExclusiveMinter is Ownable {
 
 curatedContract public mirageContract;
 membershipContracts public membershipContract;
 
 mapping(uint256 => bool) public includedProjectId;
 mapping(uint256 => bool) public mintedID;
 mapping(address => bool) public mintedWalletStandard;
 mapping(address => bool) public mintedWalletSecondary;
 mapping(address => intelAllotment) intelQuantity;
 
 address private immutable adminSigner;
 
 struct intelAllotment {
   uint256 allotment;
 }
 
 struct Coupon {
   bytes32 r;
   bytes32 s;
   uint8 v;
 }
 
 constructor(address _curatedAddress, address _membershipAddress, address _adminSigner) {
   mirageContract = curatedContract(_curatedAddress);
   membershipContract = membershipContracts(_membershipAddress);
   adminSigner = _adminSigner;
 }
 
 function purchase(uint256 _projectId) public payable {
   require(includedProjectId[_projectId], "This project cannot be minted through this contract");
   require(msg.value >= mirageContract.projectIdToPricePerTokenInWei(_projectId), "Must send minimum value to mint!");
   require(msg.sender == tx.origin, "Reverting, Method can only be called directly by user.");
   _splitFundsETH(_projectId, 1);
   mirageContract.mint(msg.sender, _projectId, msg.sender);
 }
 
 function toggleIncludedIds(uint256 _id) public onlyOwner {
   includedProjectId[_id] = !includedProjectId[_id];
 }
 
 function setIntelAllotment(address[] memory _addresses, uint256[] memory allotments) public onlyOwner {
   for(uint i = 0; i < _addresses.length; i++) {
     intelQuantity[_addresses[i]].allotment = allotments[i];
   }
 }
 
 function _isVerifiedCoupon(bytes32 digest, Coupon memory coupon) internal view returns (bool) {
   address signer = ecrecover(digest, coupon.v, coupon.r, coupon.s);
   require(signer != address(0), "ECDSA: invalid signature");
   return signer == adminSigner;
 }
 
 function viewAllotment(address _address) public view returns (uint256) {
   if (intelQuantity[_address].allotment == 99) {
     return 0;
   } else {
     return intelQuantity[_address].allotment;
   }
 }
 
 function sentientPurchase(uint256 _membershipId, uint256 _projectId) public payable {
   require(_membershipId < 50, "Enter a valid Sentient membership ID (0-49)");
   require(includedProjectId[_projectId], "This project cannot be minted through this contract");
   require(membershipContract.balanceOf(msg.sender,_membershipId) > 0, "No membership tokens in this wallet");
   require(msg.value >= mirageContract.projectIdToPricePerTokenInWei(_projectId), "Must send minimum value to mint!");
   require(!mintedID[_membershipId], "Already minted");
   mintedID[_membershipId] = true;
   _splitFundsETH(_projectId, 1);
   mirageContract.earlyMint(msg.sender, _projectId, msg.sender);
 }
 
 function intelligentPurchase(uint256 _projectId, Coupon memory coupon) public payable {
   require(includedProjectId[_projectId], "This project cannot be minted through this contract");
   require(msg.value >= mirageContract.projectIdToPricePerTokenInWei(_projectId), "Must send minimum value to mint!");
   require(msg.sender == tx.origin, "Reverting, Method can only be called directly by user.");
   uint256 allot = intelQuantity[msg.sender].allotment;
    bytes32 digest = keccak256(abi.encode(msg.sender,"member"));
     require(_isVerifiedCoupon(digest, coupon), "Invalid coupon");
    if (allot > 0) {
     require(allot != 99, "Already minted total allotment");
     uint256 updatedAllot = allot - 1;
     intelQuantity[msg.sender].allotment = updatedAllot;
     if (updatedAllot == 0) {
       intelQuantity[msg.sender].allotment = 99;
     }
   } else if (allot == 0) {
     intelQuantity[msg.sender].allotment = 99;
   }
   _splitFundsETH(_projectId, 1);
   mirageContract.earlyMint(msg.sender, _projectId, msg.sender);
 }
 
 function standardPresalePurchase(Coupon memory coupon, uint256 _projectId) public payable {
   require(msg.value >= (mirageContract.projectIdToPricePerTokenInWei(_projectId)), "Must send minimum value to mint!");
   require(includedProjectId[_projectId], "This project cannot be minted through this contract");
   require(!mintedWalletStandard[msg.sender], "Already minted");
   require(msg.sender == tx.origin, "Reverting, Method can only be called directly by user.");
   bytes32 digest = keccak256(abi.encode(msg.sender,"standard"));
     require(_isVerifiedCoupon(digest, coupon), "Invalid coupon");
   _splitFundsETH(_projectId, 1);
   mintedWalletStandard[msg.sender] = true;
   mirageContract.earlyMint(msg.sender, _projectId, msg.sender);
 }
 
 function secondaryPresalePurchase(Coupon memory coupon, uint256 _projectId) public payable {
   require(msg.value >= (mirageContract.projectIdToPricePerTokenInWei(_projectId)), "Must send minimum value to mint!");
   require(includedProjectId[_projectId], "This project cannot be minted through this contract");
   require(!mintedWalletSecondary[msg.sender], "Already minted");
   require(msg.sender == tx.origin, "Reverting, Method can only be called directly by user.");
   bytes32 digest = keccak256(abi.encode(msg.sender,"secondary"));
     require(_isVerifiedCoupon(digest, coupon), "Invalid coupon");
   _splitFundsETH(_projectId, 1);
   mintedWalletSecondary[msg.sender] = true;
   mirageContract.earlyMint(msg.sender, _projectId, msg.sender);
 }
 
 function _splitFundsETH(uint256 _projectId, uint256 numberOfTokens) internal {
   if (msg.value > 0) {
     uint256 mintCost = mirageContract.projectIdToPricePerTokenInWei(_projectId) * numberOfTokens;
     uint256 refund = msg.value - (mirageContract.projectIdToPricePerTokenInWei(_projectId) * numberOfTokens);
     if (refund > 0) {
       payable(msg.sender).transfer(refund);
     }
     uint256 mirageAmount = mintCost / 100 * mirageContract.miragePercentage();
     if (mirageAmount > 0) {
       payable(mirageContract.mirageAddress()).transfer(mirageAmount);
     }
     uint256 projectFunds = mintCost - mirageAmount;
     uint256 additionalPayeeAmount;
     if (mirageContract.projectIdToAdditionalPayeePercentage(_projectId) > 0) {
       additionalPayeeAmount = projectFunds / 100 * mirageContract.projectIdToAdditionalPayeePercentage(_projectId);
       if (additionalPayeeAmount > 0) {
         payable(mirageContract.projectIdToAdditionalPayee(_projectId)).transfer(additionalPayeeAmount);
       }
     }
     uint256 creatorFunds = projectFunds - additionalPayeeAmount;
     if (creatorFunds > 0) {
       payable(mirageContract.projectIdToArtistAddress(_projectId)).transfer(creatorFunds);
     }
   }
 }
}
 

